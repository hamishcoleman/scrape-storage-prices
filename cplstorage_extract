#!/usr/bin/env perl
use warnings;
use strict;
#
# Grab the hard drives data from CPL
#

# allow the libs to be in the bin dir
use FindBin;
use lib "$FindBin::RealBin/lib";
use lib "/home/hamish/s/bin/lib";

use ScrapeHelper;
use ScrapeExtractStorage;

sub scrape1product {
    my $node = shift;
    my $data = {};

    my $url = $node->look_down(
        '_tag', 'a',
        'class', 'product-item-link',
    );
    if (!$url) {
        die("Could not find product url");
        return undef;
    }

    $data->{comment} = $url->as_trimmed_text();
    $data->{url} = $url->attr('href');
    $data->{supplier} = 'CPL';
    
    $data->{price} = ScrapeHelper::look_down2text(
        $node,
        '_tag', 'span',
        'class', 'price'
    );

    return $data;
}

sub scrape2products {
    my $tree = shift;
    my $timestamp = shift;

    my $products = $tree->look_down(
        '_tag', 'div',
        'class', 'products wrapper grid products-grid',
    );
    if (!$products) {
        die("Could not find products");
        return undef;
    }

    my @products;
    for my $node ($products->look_down(
            '_tag', 'li',
            'class', qr/item/,
        )) {
        my $data = scrape1product($node);
        $data->{timestamp} = $timestamp;
        push @products, $data;
    }

    return @products;
}

# HACK!
# - CPL might fix their HTML, if so, move this back into the library
# - if not, figure out a better solution
#
use HTML::TreeBuilder;
use IO::HTML;
sub file2tree {
    my $file = shift;

    my $fh = html_file($file);
    return undef if (!$fh);

    # apply hack filter
    my $txt = '';
    my $found_bad = 0;
    while (<$fh>) {
        if (m/<ul class="add-to-links">/) {
            $found_bad = 1;
            next;
        }
        if ($found_bad && m%</ul>%) {
            $found_bad = 0;
            next;
        }
        if ($found_bad) {
            next;
        }
        $txt .= $_;
    }

    my $tree = HTML::TreeBuilder->new;
    $tree->ignore_unknown(0);
    $tree->parse($txt);
    $tree->eof;
    $tree->elementify;

    return $tree;
}

sub load_table_ignore {
    my $filename = 'cplstorage_ignore.txt';
    my $fh;
    if (!open($fh, "<", $filename)) {
        warn("Could not open $filename");
        return;
    }

    my $table_ignore = ScrapeExtractStorage::table_ignore();
    while (<$fh>) {
        chomp;
        $table_ignore->{$_} = 1;
    }
}

sub main {
    if (scalar(@ARGV) <2) {
        print("Usage: $0 outfile infile...\n");
        exit(1);
    }
    my $outfilename = shift @ARGV;

    load_table_ignore();

    my @products;
    for my $file (@ARGV) {
        my $mtime = (stat($file))[9];
        my $tree = file2tree($file);
        print("Processing ",$file,"\n");
        push @products, scrape2products($tree, $mtime);
    }

    @products = ScrapeExtractStorage::extractproducts(@products);

    my @fields = qw(price form interface speed gig sgig brand supplier comment);

    ScrapeHelper::outputcsv($outfilename, \@fields, @products);

    print("Match counts:\n");
    my $matchtable = ScrapeExtractStorage::matchtable();
    for my $re (sort(keys(%{$matchtable}))) {
        print($matchtable->{$re}{_matches}||0,"\t'",$re,"'\n");
    }
}

main();
